//Compares two link tables and reports which links only exist in one of the tables.

OUTER_SPRINT table1 "test/links.tbl"
OUTER_SPRINT table2 "test/new_map_mods_links_bgt.tbl"
OUTER_SPRINT output_file "test/link_table_output.txt"

COPY "%table1%" "%table1%"
  READ_2DA_ENTRIES_NOW r2en_table 12
  FOR (i = 0; i < r2en_table; ++i) BEGIN
    READ_2DA_ENTRY_FORMER r2en_table i 0 src
    READ_2DA_ENTRY_FORMER r2en_table i 1 j
    READ_2DA_ENTRY_FORMER r2en_table i 2 trgt
    TO_LOWER src
    TO_LOWER j
    TO_LOWER trgt
    SET $table1_links("%src%" "%j%" "%trgt%") = 1
  END
BUT_ONLY

COPY "%table2%" "%table2%"
  READ_2DA_ENTRIES_NOW r2en_table 12
  FOR (i = 0; i < r2en_table; ++i) BEGIN
    READ_2DA_ENTRY_FORMER r2en_table i 0 src
    READ_2DA_ENTRY_FORMER r2en_table i 1 j
    READ_2DA_ENTRY_FORMER r2en_table i 2 trgt
    TO_LOWER src
    TO_LOWER j
    TO_LOWER trgt
    SET $table2_links("%src%" "%j%" "%trgt%") = 1
  END
BUT_ONLY

OUTER_SPRINT out "Links that exist in table 1 but do not exist in table 2:%LNL%"

ACTION_PHP_EACH table1_links AS i => int BEGIN
  ACTION_IF !VARIABLE_IS_SET $table2_links("%i_0%" "%i_1%" "%i_2%") BEGIN
    OUTER_SPRINT out "%out%%i_0% %i_1% %i_2%%LNL%"
  END
END

OUTER_SPRINT out "%out%%LNL%%LNL%Links that exist in table 2 but do not exist in table 1:%LNL%"

ACTION_PHP_EACH table2_links AS i => int BEGIN
  ACTION_IF !VARIABLE_IS_SET $table1_links("%i_0%" "%i_1%" "%i_2%") BEGIN
    OUTER_SPRINT out "%out%%i_0% %i_1% %i_2%%LNL%"
  END
END

<<<<<<<< ...blank
>>>>>>>>

ACTION_IF STRING_LENGTH "%out%" > 0 BEGIN
  COPY "...blank" "%output_file%"
    INSERT_BYTES 0 STRING_LENGTH "%out%"
    WRITE_ASCIIE 0 "%out%"

  PRINT "Output written to %output_file%"
END ELSE BEGIN
  PRINT "No differences detected"
END
