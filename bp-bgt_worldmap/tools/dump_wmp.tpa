//Dumps the areas and links of a .wmp file to textual data

OUTER_SPRINT area_file "test/areas.2da"
OUTER_SPRINT link_file "test/links.2da"

<<<<<<<< .../fl-inlined/bp-bgt-worldmap/blank


>>>>>>>>

OUTER_SPRINT area_string "SHORT_NAME CONTENT LONG_NAME FLAGS BAM_ANIM X_POS Y_POS NAME TOOLTIP LOAD_IM%LNL%"
OUTER_SPRINT link_string "SRC_AREA SRC_NWSE TARGET_ARE ENTRY_NAME TRV_TIME DEF_ENTRY ENC1 ENC2 ENC3 ENC4 ENC5 ENC_PROB%LNL%"

COPY_EXISTING worldmap.wmp override
  READ_LONG 0x30 na
  READ_LONG 0x34 ao
  READ_LONG 0x38 lo
  FOR (i=0;i<na;++i) BEGIN
    READ_ASCII ao + 0xf0*i area
    READ_LONG  ao + 0xf0*i + 0x30 flags
    READ_LONG  ao + 0xf0*i + 0x34 bam
    READ_LONG  ao + 0xf0*i + 0x38 xcor
    READ_LONG  ao + 0xf0*i + 0x3c ycor
    READ_STRREF ao + 0xf0*i + 0x40 name
    READ_STRREF ao + 0xf0*i + 0x44 tool
    READ_ASCII  ao + 0xf0*i + 0x48 load
    PATCH_FOR_EACH var IN name tool BEGIN
      SPRINT content EVAL "%%var%%"
      INNER_PATCH_SAVE content "%content%" BEGIN
        REPLACE_TEXTUALLY " " "###"
      END
      SPRINT EVAL "%var%" "%content%"
    END
    PATCH_FOR_EACH var IN name tool load BEGIN
      SPRINT content EVAL "%%var%%"
      PATCH_IF "%content%" STRING_MATCHES_REGEXP "[a-z0-9_']+" BEGIN
        //PATCH_PRINT "%content% is replaced with N"
        SPRINT EVAL "%var%" N
      END
    END
    SPRINT area_string "%area_string%%area% %area% %area% %flags% %bam% %xcor% %ycor% %name% %tool% %load%%LNL%"
    PATCH_FOR_EACH j IN 0 3 2 1 BEGIN
      READ_LONG ao + 0xf0*i + 0x50 + 0x8*j li
      READ_LONG ao + 0xf0*i + 0x54 + 0x8*j nl
      FOR (k=0;k<nl;++k) BEGIN
        READ_LONG lo + 0xd8*(li + k) target
        READ_ASCII ao + 0xf0*target target_are
        READ_ASCII lo + 0xd8*(li + k) + 0x4 entry (32) NULL
        READ_LONG  lo + 0xd8*(li + k) + 0x24 time
        READ_LONG  lo + 0xd8*(li + k) + 0x28 flags
        READ_ASCII lo + 0xd8*(li + k) + 0x2c enc1
        READ_ASCII lo + 0xd8*(li + k) + 0x34 enc2
        READ_ASCII lo + 0xd8*(li + k) + 0x3c enc3
        READ_ASCII lo + 0xd8*(li + k) + 0x44 enc4
        READ_ASCII lo + 0xd8*(li + k) + 0x4c enc5
        READ_LONG  lo + 0xd8*(li + k) + 0x54 prob
        PATCH_FOR_EACH var IN entry enc1 enc2 enc3 enc4 enc5 BEGIN
          SPRINT content EVAL "%%var%%"
          PATCH_IF "%content%" STRING_MATCHES_REGEXP "[a-z0-9_']+" BEGIN
            PATCH_PRINT "%content% is replaced with N"
            SPRINT content "N"
          END
          INNER_PATCH_SAVE content "%content%" BEGIN
            REPLACE_TEXTUALLY " " "###"
          END
          SPRINT EVAL "%var%" "%content%"
        END
        INNER_PATCH_SAVE nesw "%j%" BEGIN
          REPLACE_TEXTUALLY 0 N
          REPLACE_TEXTUALLY 1 W
          REPLACE_TEXTUALLY 2 S
          REPLACE_TEXTUALLY 3 E
        END
        SPRINT link_string "%link_string%%area% %nesw% %target_are% %entry% %time% %flags% %enc1% %enc2% %enc3% %enc4% %enc5% %prob%%LNL%"
      END
    END
    SPRINT link_string "%link_string%%LNL%%LNL%"
  END
BUT_ONLY

COPY ".../fl-inlined/bp-bgt-worldmap/blank" "%area_file%"
  INSERT_BYTES 0 STRING_LENGTH "%area_string%"
  WRITE_ASCIIE 0 "%area_string%"
  PRETTY_PRINT_2DA

COPY ".../fl-inlined/bp-bgt-worldmap/blank" "%link_file%"
  INSERT_BYTES 0 STRING_LENGTH "%link_string%"
  WRITE_ASCIIE 0 "%link_string%"
  PRETTY_PRINT_2DA
